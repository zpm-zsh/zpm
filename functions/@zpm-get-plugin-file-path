#!/usr/bin/env zsh

local Plugin="${1}"
local Plugin_path="${2}"
local Plugin_basename="${3}"

# Workaround for zsh-syntax-highlighting
if [[ "$Plugin" == 'zsh-users/zsh-syntax-highlighting' ]]; then
  echo "${Plugin_path}/zsh-syntax-highlighting.zsh"
  return 0
fi

# Workaround for zsh-history-substring-search
if [[ "$Plugin" == 'zsh-users/zsh-history-substring-search' ]]; then
  echo "${Plugin_path}/zsh-history-substring-search.zsh"
  return 0
fi

local Plugin_basename_clean=$Plugin_basename
Plugin_basename_clean=${Plugin_basename_clean##oh-my-zsh-}
Plugin_basename_clean=${Plugin_basename_clean##zsh-}
Plugin_basename_clean=${Plugin_basename_clean%%.plugin}
Plugin_basename_clean=${Plugin_basename_clean%%-plugin}
Plugin_basename_clean=${Plugin_basename_clean%%.zsh}
Plugin_basename_clean=${Plugin_basename_clean%%-zsh}
Plugin_basename_clean=${Plugin_basename_clean%%.plugin}
Plugin_basename_clean=${Plugin_basename_clean%%-plugin}

if [[ "$Plugin" == *",source:"* ]]; then
  local plugin_sourcefile="${${Plugin##*,source:}%%,*}"
  if [[ -n "$plugin_sourcefile" && -e "${Plugin_path}/${plugin_sourcefile}" ]]; then
    echo "${Plugin_path}/${plugin_sourcefile}"
    return 0
  fi
  return -1
fi

local -a candidate_files=(
  "${Plugin_basename_clean}.zsh"
  "zsh-${Plugin_basename_clean}.zsh"
  "${Plugin_basename_clean}.plugin.zsh"
  "zsh-${Plugin_basename_clean}.plugin.zsh"
  "${Plugin_basename_clean}.zsh-theme"
  "init.zsh"
)

local candidate
for candidate in "${candidate_files[@]}"; do
  local candidate_path="${Plugin_path}/${candidate}"
  if [[ -e "$candidate_path" ]]; then
    echo "$candidate_path"
    return 0
  fi
done

return -1
