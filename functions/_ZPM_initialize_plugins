#!/usr/bin/env zsh

function _ZPM_initialize_plugins() {
  # Check if plugin isn't loaded
  local -a _Plugins_Install
  local -A _Plugins_Load

  for plugin in $@; do
    local Plugin_name=$(_ZPM_get_plugin_name "$plugin")

    if [[ ! -z $_ZPM_plugins_full[$Plugin_name] ]]; then
      _ZPM_log zpm:init:skip "Skip initialization '$Plugin_name', plugin already loaded"
    else
      _ZPM_plugins_full[$Plugin_name]="$plugin"

      local Plugin_path=$(_ZPM_get_plugin_path "$Plugin_name")

      if [[ ! -e $Plugin_path ]]; then
        _Plugins_Install+=("$plugin")
      fi

      _Plugins_Load[$Plugin_name]="$plugin"
    fi
  done

  if [[ -n "$_Plugins_Install[@]" ]]; then;
    printf '%s\0' "${_Plugins_Install[@]}" | \
      xargs -0 -P16 -n1 "${_ZPM_DIR}/bin/_ZPM-plugin-helper" install
  fi

  for plugin_name plugin in ${(kv)_Plugins_Load}; do
    _ZPM_load_plugin "$plugin" "$plugin_name"
  done
}
