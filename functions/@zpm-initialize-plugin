#!/usr/bin/env zsh

local Plugin="$1"
local Plugin_name="$2"
local Plugin_basename=$(@zpm-get-plugin-basename "$Plugin_name")
local Plugin_path=$(@zpm-get-plugin-path "$Plugin_name")

local zpm_plugin_autoload=''

local zpm_plugin_source=false
local zpm_plugin_async=false
local zpm_plugin_bin_path=false
local zpm_plugin_functions_path=false

local apply_spec="source:path:fpath"
if [[ "$Plugin" == *",apply:"* ]]; then
  apply_spec="${${Plugin##*,apply:}%%\,*}"
fi
local apply_flags=":${apply_spec}:"

if [[ "$apply_flags" == *":source:"* ]]; then
  zpm_plugin_source=true
fi

if [[ "$apply_flags" == *":path:"* ]]; then
  zpm_plugin_bin_path=true
fi

if [[ "$apply_flags" == *":fpath:"* ]]; then
  zpm_plugin_functions_path=true
fi

if [[ ",$Plugin," == *",async,"* ]]; then
  zpm_plugin_async=true
fi

if [[ -f "${Plugin_path}/plugin-options.zsh" ]]; then
  source "${Plugin_path}/plugin-options.zsh"
fi

@zpm-log zpm:init "Initialize '${Plugin_name}'"

# Fpath
if [[ "$zpm_plugin_functions_path" == "true" ]]; then
  local Plugin_functions_path=$(@zpm-get-plugin-functions-path "$Plugin" "$Plugin_path")
  if [[ -d "${Plugin_functions_path}" ]]; then
    @zpm-log zpm:init:fpath "Add to \$fpath '${Plugin_functions_path}'"
    @zpm-addfpath "${Plugin_functions_path}"
  fi
fi

# Bin path
if [[ "$zpm_plugin_bin_path" == "true" ]]; then
  local Plugin_bin_path=$(@zpm-get-plugin-bin-path "$Plugin" "$Plugin_path")
  if [[ -d "$Plugin_bin_path" ]]; then
    @zpm-log zpm:init:path "Add to \$PATH '${Plugin_bin_path}'"
    @zpm-addpath "${Plugin_bin_path}"
  fi
fi

# Source plugin
if [[ "$zpm_plugin_source" == "true" ]]; then
  local _ZPM_plugin_file_path=$( @zpm-get-plugin-file-path "${Plugin}" "${Plugin_path}" "${Plugin_basename}" )
  if [[ -n "$_ZPM_plugin_file_path" ]]; then
    if [[ "$zpm_plugin_async" == "true" ]]; then
      @zpm-async-source "${Plugin_name}" "${_ZPM_plugin_file_path}"
    else
      @zpm-source "${Plugin_name}" "${_ZPM_plugin_file_path}"
    fi
  else
    @zpm-no-source "${Plugin_name}" 'missing'
  fi
else
  @zpm-no-source "${Plugin_name}" 'disabled'
fi

# Autoload
local Plugin_autoload_files=$(@zpm-get-plugin-autoload "$Plugin" "$zpm_plugin_autoload")
if [[ -n "$Plugin_autoload_files" ]]; then
  @zpm-log zpm:init:autoload "Autoload ${Plugin_autoload_files}"
  @zpm-add-autoload "$Plugin_autoload_files"
fi
