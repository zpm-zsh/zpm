#!/usr/bin/env zsh

# Check if plugin isn't loaded
local -a plugins_install=()
local -a plugins_load_order=()
local -A plugins_load=()

for plugin in "$@"; do
  local plugin_name=$(@zpm-get-plugin-name "$plugin")

  if [[ -n ${_ZPM_plugins_full[$plugin_name]-} ]]; then
    @zpm-log zpm:init:skip "Skip initialization '${plugin_name}', plugin already loaded"
    continue
  fi

  _ZPM_plugins_full[$plugin_name]="$plugin"

  local plugin_path=$(@zpm-get-plugin-path "$plugin_name")
  if [[ ! -e "$plugin_path" ]]; then
    plugins_install+=("$plugin")
  fi

  plugins_load_order+=("$plugin_name")
  plugins_load[$plugin_name]="$plugin"
done

if (( ${#plugins_install} )); then
  @zpm-launch-plugin-helper install "${plugins_install[@]}"
fi

for plugin_name in "${plugins_load_order[@]}"; do
  @zpm-initialize-plugin "${plugins_load[$plugin_name]}" "$plugin_name"
done
