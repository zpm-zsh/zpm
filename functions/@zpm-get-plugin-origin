#!/usr/bin/env zsh

local plugin_spec="$1"
local plugin_name="$2"
local plugin_basename="$3"
local plugin_type="$4"


local plugin_stripped_name="${plugin_name#@*/}"

if [[ "$plugin_spec" == *",origin:"* ]]; then
  print -r -- "${${plugin_spec##*,origin:}%%,*}"
  return 0
fi

local github_url="${GITHUB_MIRROR:-https://github.com}"
local gitlab_url="${GITLAB_MIRROR:-https://gitlab.com}"
local bitbucket_url="${BITBUCKET_MIRROR:-https://bitbucket.com}"
local omz_path

case "$plugin_type" in
  github)
    print -r -- "${github_url}/${plugin_stripped_name}"
    ;;
  gitlab)
    print -r -- "${gitlab_url}/${plugin_stripped_name}"
    ;;
  bitbucket)
    print -r -- "${bitbucket_url}/${plugin_stripped_name}"
    ;;
  omz-core)
    print -r -- "${github_url}/ohmyzsh/ohmyzsh"
    ;;
  omz)
    omz_path=$(@zpm-get-plugin-path @omz)
    print -r -- "${omz_path}/plugins/${plugin_basename}"
    ;;
  omz-theme|omz-theme-old)
    omz_path=$(@zpm-get-plugin-path @omz)
    print -r -- "${omz_path}/themes/${plugin_basename}.zsh-theme"
    ;;
  omz-lib|omz-lib-old)
    omz_path=$(@zpm-get-plugin-path @omz)
    print -r -- "${omz_path}/lib/${plugin_basename}.zsh"
    ;;
  gist)
    print -r -- "https://gist.githubusercontent.com/${plugin_stripped_name}/raw"
    ;;
  *)
    return -1
    ;;
esac
